#!/usr/bin/awk -f
#
# Copyright (c) 2012,2015 Douglas G. Scofield, Uppsala University
# douglas.scofield@ebc.uu.se
# douglasgscofield@gmail.com
#
# This code is covered by the Gnu GPLv2, please see LICENSE.
# Bugs and suggestions to https://github.com/douglasgscofield/tinyutils/issues.
#
# Only pass along data lines for which column values fall within the specified range,
# except with inverse=1
#
# CHANGELOG
# 2017-08-10 : add inverse option
# 2015-03-31 : fix bug with abs=: constrain option processing to first input line
# 2014-05-27 : don't quit on missing-column error
# 2013-05-15 : make FS and OFS be "\t" by default
# 2012-12-04 : create the script


BEGIN { 
    # parameters
    FS = "\t"; 
    OFS = "\t"; 
    abs = "undef";
    min = "undef";
    max = "undef";
    header = 0;        # does the input have a header? if so how many lines?
    skip_comment = 1;  # skip '#' lines on input
    skip_missing = 1;  # skip lines without the column
    col = 1;           # which column do we operate on?
    inverse = 0;       # invert the sense of the test: all but the lines that are in range
}

{
    if (NR == 1) {
        if (abs != "undef" && min == "undef" && max == "undef") {
            if (abs < 0) {
                print "abs= must be >= 0";
                exit 1;
            }
            max = abs; min = -abs; abs = "undef";
        } else if ((abs != "undef" && (min != "undef" || max != "undef")) ||
                   (abs == "undef" && min == "undef" && max == "undef")) {
            print "specify min=, max=, both min= and max=, or abs= (absolute value of range) on the command line";
            exit 1;
        }
    }

    if (NR <= header) { print; next; }
    if (skip_comment && $0 ~ /^#/) { print; next; }

    if (NF < col) {
        print "on input line " NR " missing requested column" > "/dev/stderr";
        if (skip_missing) {
            next;
        } else {
            print;
            next;
        }
    }
    if ((min == "undef" || $(col) >= min) && (max == "undef" || $(col) <= max)) {
        if (inverse == 0)
            print;
    } else if (inverse == 1)
        print;
}

