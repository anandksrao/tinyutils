#!/usr/bin/awk -f
#
# Copyright (c) 2014 Douglas G. Scofield, Uppsala University
# douglas.scofield@ebc.uu.se
# douglasgscofield@gmail.com
#
# This code is covered by the Gnu GPLv2, please see LICENSE.
# Bugs and suggestions to https://github.com/douglasgscofield/tinyutils/issues.
#
# Round the value of a set of numbers
#
# CHANGELOG
# 2014-05-27 : don't quit on missing-column error
# 2014-02-25 : first version


BEGIN { 
    FS = "\t"; 
    OFS = "\t"; 
    # parameters
    n = 0;
    header = 0;        # does the input have a header? if so how many lines?
    skip_comment = 1;  # skip '#' lines on input
    col = 1;           # which column do we operate on?
    digits = 3
}

function round(D, N) {
    f = 10 ^ N;
    return(int((D * f) + 0.5) / f);
}

{
    if (NR <= header) { print; next; }
    if (skip_comment && $0 ~ /^#/) { print; next; }

    if (NF < col) {
        print "on input line " NR " missing requested column" > "/dev/stderr";
        next;
    }

    $(col) = round($(col), digits);
    print;
}
