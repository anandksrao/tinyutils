#!/usr/bin/awk -f
#
# Copyright (c) 2012,2014 Douglas G. Scofield, Uppsala University
# douglas.scofield@ebc.uu.se
# douglasgscofield@gmail.com
#
# No warranty is implied or assumed by this code.  Please see LICENSE.
# Bugs and suggestions to https://github.com/douglasgscofield/tinyutils/issues.
#
# Compute the successive differences between numbers in a column: 2nd - 1st, 3rd - 2nd, etc.
#
# CHANGELOG
# 2014-05-27 : don't quit on missing-column error
# 2013-05-15 : make FS and OFS be "\t" by default
# 2012-12-03 : clean the script up a bit


BEGIN { 
    # parameters
    FS = "\t"; 
    OFS = "\t"; 
    prev = "undef";
    header = 0;        # does the input have a header? if so how many lines?
    skip_comment = 1;  # skip '#' lines on input
    col = 1;           # which column do we operate on?
}

{
    if (NR <= header) { next; }
    if (skip_comment && $0 ~ /^#/) { next; }

    if (NF < col) {
        print "on input line " NR " missing requested column" > "/dev/stderr";
        next;
    }
    if (prev != "undef") 
        print $(col) - prev;
    prev = $(col);
}

