#!/usr/bin/awk -f
#
# Copyright (c) 2014 Douglas G. Scofield, Uppsala University
# douglas.scofield@ebc.uu.se
# douglasgscofield@gmail.com
#
# This code is covered by the Gnu GPLv2, please see LICENSE.
# Bugs and suggestions to https://github.com/douglasgscofield/tinyutils/issues.
#
# Compute the min and max of a set of numbers
#
# CHANGELOG
# 2014-11-13 : create script


BEGIN { 
    # parameters
    n = 0;
    min = +1.0E+300
    max = -1.0E+300
    header = 0;        # does the input have a header? if so how many lines?
    skip_comment = 1;  # skip '#' lines on input
    col = 1;           # which column do we operate on?
}

{
    if (NR <= header) { print; next; }
    if (skip_comment && $0 ~ /^#/) { print; next; }

    if (NF < col) {
        print "on input line " NR " missing requested column" > "/dev/stderr";
        next;
    }
    if ($(col) > max)
        max = $(col);
    if ($(col) < min)
        min = $(col);
    ++n;
}

END {
    if (n)
        print min "\t" max;
}
