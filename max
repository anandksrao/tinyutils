#!/usr/bin/awk -f
#
# Copyright (c) 2012 Douglas G. Scofield, Umeå Plant Sciences Centre, Umeå, Sweden
# douglas.scofield@plantphys.umu.se
# douglasgscofield@gmail.com
#
# No warranty is implied or assumed by this code.  Please send bugs, suggestions etc.
#
# Compute the max of a set of numbers
#
# CHANGELOG
# 2014-05-27 : don't quit on missing-column error
# 2012-12-03 : clean the script up a bit


BEGIN { 
    # parameters
    n = 0;
    max = -1.0E+300
    header = 0;        # does the input have a header? if so how many lines?
    skip_comment = 1;  # skip '#' lines on input
    col = 1;           # which column do we operate on?
}

{
    if (NR <= header) { print; next; }
    if (skip_comment && $0 ~ /^#/) { print; next; }

    if (NF < col) {
        print "on input line " NR " missing requested column" > "/dev/stderr";
        next;
    }
    if ($(col) > max)
        max = $(col);
    ++n;
}

END {
    if (n)
        print max;
}
