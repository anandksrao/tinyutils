#!/usr/bin/awk -f
#
# Copyright (c) 2012 Douglas G. Scofield, Umeå Plant Sciences Centre, Umeå, Sweden
# douglas.scofield@plantphys.umu.se
# douglasgscofield@gmail.com
#
# No warranty is implied or assumed by this code.  Please send bugs, suggestions etc.
#
# Turns a column into a boolean value (0 or 1)
#
# CHANGELOG
# 2014-05-27 : don't quit on missing-column error
# 2013-05-15 : make FS and OFS be "\t" by default
# 2012-12-02 : clean the script up a bit
# 2012-11-27 : create the script


BEGIN { 
    # parameters
    FS = "\t"; 
    OFS = "\t"; 
    header = 0;        # does the input have a header? if so how many lines?
    skip_comment = 1;  # skip '#' lines on input
    false = 0;         # set to the value that should be false
    # min              # set min if you want the script to assume input values 
                       # are numeric, it will only make values true that
                       # are >= min.
    col = 1;           # which column do we boolify?
}

{
    if (NR <= header) { print; next; }
    if (skip_comment && $0 ~ /^#/) { print; next; }

    if (NF < col) {
        print "on input line " NR " missing requested column" > "/dev/stderr";
        next;
    }
    if (min) {
        if ($(col) >= min) {
            $(col) = 1;
        } else {
            $(col) = 0;
        }
    } else if ($(col) != false) {
        $(col) = 1;
    } else {
        $(col) = 0;
    }
    print;
}
